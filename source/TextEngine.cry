import Graphics2D;
import ImageResources;
import Math;

class TextEngine {
	static field LETTERS = {};
	static field CODE_LOOKUP = {};
	
	static constructor() {
		TextEngine.CODE_LOOKUP = {
			':': 'co',
			',': 'cm',
			'}': 'cc',
			']': 'cb',
			'>': 'ca',
			'&': 'am',
			"'": 'ap',
			'*': 'as',
			'@': 'at',
			'^': 'cr',
			')': 'cp',
			'=': 'eq',
			'%': 'pt',
			'~': 'ti',
			'|': 'pi',
			'+': 'pl',
			'$': 'do',
			'-': 'hy',
			'.': 'pe',
			'`': 'bt',
			'\\': 'bs',
			'!': 'ex',
			'#': 'ha',
			'"': 'dq',
			'?': 'qu',
			'{': 'oc',
			'[': 'ob',
			'<': 'oa',
			'_': 'un',
			'/': 'sl',
			';': 'se',
			'(': 'op',
		};
		for (letter : "abcdefghijklmnopqrstuvwyxz") {
			TextEngine.CODE_LOOKUP[letter] = 'lc_' + letter;
			TextEngine.CODE_LOOKUP[letter.upper()] = 'uc_' + letter;
		}
		for (i = 0; i < 10; ++i) {
			TextEngine.CODE_LOOKUP['' + i] = 'n' + i;
		}
	}
	
	static function render(text, size, x, y) {
		charMap = TextEngine.getCharMap(size);
		v = charMap['v'];
		spaceWidth = v.width;
		length = text.length;
		x = Math.floor(x);
		y = Math.floor(y) - v.height / 2;
		for (char : text) {
			if (char == ' ') {
				x += spaceWidth;
			} else {
				img = charMap.get(char);
				if (img == null) {
					img = charMap['?'];
				}
				img.draw(x, y);
				x += img.width;
			}
		}
	}
	
	static function getCharMap(size) {
		lookup = TextEngine.LETTERS.get(size);
		if (lookup != null) return lookup;
		
		lookup = {};
		if (size == 'O') {
			for (char : TextEngine.CODE_LOOKUP.keys()) {
				code = TextEngine.CODE_LOOKUP[char];
				// already loaded by image sheet so synchronous access is ok.
				image = ImageLibrary.get('text/' + code + '.png');
				lookup[char] = image;
			}
			TextEngine.LETTERS['O'] = lookup;
		} else {
			xl = TextEngine.getCharMap('O');
			for (key : xl.keys()) {
				img = xl[key];
				w = img.width;
				h = img.height;
				switch (size) {
					case 'XL': r = 1.0; break;
					case 'L': r = 20 / 36.0; break;
					case 'M': r = 16 / 36.0; break;
					case 'S': r = 12 / 36.0; break;
					default: assert(false); break;
				}
				
				r *= 1.5;
				
				w = floor(w * r);
				h = floor(h * r);
				lookup[key] = img.scale(w, h);
			}
			TextEngine.LETTERS[size] = lookup;
		}
		
		return lookup;
	}
}
